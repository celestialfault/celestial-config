plugins {
	id 'java'
	id("maven-publish")
}

group = 'me.celestialfault'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.jetbrains:annotations:24.0.0'
	implementation 'com.google.code.gson:gson:2.10.1'
}

java {
	targetCompatibility = 8
	sourceCompatibility = 8
	withSourcesJar()
	withJavadocJar()
}

publishing {
	repositories {
		maven {
			name = "celestialfault"
			url = uri("https://maven.odinair.xyz/releases")
			credentials {
				username = project.findProperty("maven.username") ?: System.getenv("MAVEN_NAME")
				password = project.findProperty("maven.secret") ?: System.getenv("MAVEN_SECRET")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			from(components.java)
		}
	}
}

tasks.withType(Javadoc).configureEach {
	options.tags = [
		'apiNote:a:<em>API Note:</em>',
		'implSpec:a:<em>Implementation Requirements:</em>',
		'implNote:a:<em>Implementation Note:</em>'
	]
}
